#
# A simple theme from MidnightRain.
#
# Authors:
#   Taian Su <taiansu@gmail.com>
#
# Screenshots:
#   http://
#

function +vi-git-status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]=' %F{3}✖%f'
  fi
}

function prompt_midnight_rain_precmd {
  vcs_info

  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_midnight_rain_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_midnight_rain_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' get-revision true
  zstyle ':vcs_info:*' use-simple true
  zstyle ':vcs_info:*' stagedstr ' %F{green}⏏%f'
  zstyle ':vcs_info:*' unstagedstr ' %F{004}✚%f'
  zstyle ':vcs_info:*' formats '[ %F{white}%b%f %F{8}%.7i%f%c%u ]'
  zstyle ':vcs_info:*' actionformats ' %F{8}%b%f %F{white}%.7i%f +%a%u'
  zstyle ':vcs_info:git*+set-message:*' hooks git-status

  # Set ruby-info parameters.
  zstyle ':prezto:module:ruby:info:version' format '%F{white}»%v%f'

  # Define prompts.
  PROMPT="
%(?.%F{237}${1:-⎇ }%f.%F{009}${1:-⌥ }%f)%F{237}==========================================================%f
%F{067}%~%f "'${vcs_info_msg_0_}'"
%F{067}%(!.#.❯)%f "

  RPROMPT='${ruby_info[version]}%F{8}@%n%f'

}

function prompt_midnight_rain_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt midnight_rain [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_midnight_rain_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 'midnight_rain' "$@"
  else
    prompt_preview_theme 'midnight_rain'
    print
    prompt_preview_theme 'midnight_rain' "❯"
    print
    prompt_preview_theme 'midnight_rain' "$"
  fi
}

prompt_midnight_rain_setup "$@"

